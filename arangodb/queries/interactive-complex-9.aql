// Q9. Recent messages by friends or friends of friends
/*
:param [{ personId, maxDate }] => { RETURN
  4398046511268 AS personId,
  1289908800000 AS maxDate
}
*/

// 1. find all friends, and friends of friends
WITH Message
LET friends=(FOR p IN Person
FILTER p._key == @personId
  FOR f IN 1..2 ANY p knows
  FILTER f._key != p._key
  COLLECT distinct_user = f
  RETURN distinct_user)

FOR f IN friends
    FOR m IN 1..1 INBOUND f hasCreator
    FILTER m.creationDate < @maxDate
    SORT TO_NUMBER(m.creationDate) DESC, TO_NUMBER(m._key) ASC
    LIMIT @limit

    RETURN {
      "personId": f._key,
      "personFirstName": f.firstName,
      "personLastName": f.lastName,
      "commentOrPostId": m._key,
      "commentOrPostContent": m.content,
      "commentOrPostCreationDate": m.creationDate
    }

