// Q4. New topics
/*
:param [{ personId, startDate, endDate }] => { RETURN
  4398046511333 AS personId,
  1275350400000 AS startDate,
  1277856000000 AS endDate
}
*/

// find friends
WITH Place, Message, Tag
LET friends = (FOR p IN Person
FILTER p._key == @personId
  FOR f IN 1..1 ANY p knows
  FILTER f._key != p._key
  RETURN f
)

// find all posts
LET posts = (FOR f IN friends
  FOR post IN 1..1 INBOUND f hasCreator
    FILTER post.type == "post" && post.creationDate < @endDate
    RETURN post
)

LET negTags = (FOR post IN posts
  FILTER post.creationDate < @startDate
  FOR tag IN 1..1 OUTBOUND post hasTag
    RETURN DISTINCT tag
)

FOR post IN posts
  FILTER post.creationDate >= @startDate
  FOR tag IN 1..1 OUTBOUND post hasTag
    FILTER tag NOT IN negTags
    COLLECT tagName=tag.name WITH COUNT INTO postCount
      SORT postCount DESC, tagName
      LIMIT @limit
      RETURN {
        "tagName": tagName,
        "postCount": postCount
      }

