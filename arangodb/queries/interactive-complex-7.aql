// Q7. Recent likers
/*
:param personId: 4398046511268
*/

WITH Message
LET likes2 =(FOR p IN Person
FILTER p._key == @personId
  FOR post IN 1..1 INBOUND p hasCreator
    FOR liker, elike IN 1..1 INBOUND post likes
      COLLECT l=liker INTO groups = {
        "post": post,
        "likesCreationDate": elike.creationDate
      }

      return {
        "liker": l,
        "m": FIRST(FOR g IN groups SORT g.likesCreationDate DESC LIMIT 1 RETURN g)
      }
)

FOR l IN likes2
    LET liker = l.liker
    LET likesCreationDate = l.m.likesCreationDate
    LET post = l.m.post
    SORT likesCreationDate DESC, TO_NUMBER(post._key) ASC
    LIMIT 20
    RETURN {
      "friendId": liker._key,
      "friendFirstName": liker.firstName,
      "friendLastName": liker.lastName,
      "likesCreationDate": likesCreationDate,
      "minutesLatency": (likesCreationDate - post.creationDate)/1000/60,
      "messageId": post._key,
      "messageContent": post.content,
      "isNew": true
    }